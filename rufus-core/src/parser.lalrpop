use std::str::FromStr;
use lalrpop_util::ParseError;

use crate::syntax::*;

grammar;

pub Expr: Expr = {
    SumExpr,
    "fun" <Name+> "->" <Box<Expr>> => Expr::Lam(<>),
    "let" <Name> "=" <Box<Expr>> "in" <Box<Expr>> => Expr::Let(<>),
    "if" <Box<Expr>> "then" <Box<Expr>> "else" <Box<Expr>> => Expr::If(<>),
    "let" "rec" <f:Name> "=" "fun" <xs:Name+> "->" <e1:Box<Expr>> "in" <e2:Box<Expr>> => {
        let mut ys = vec![f.clone()];
        ys.extend(xs);
        let lam = Expr::Lam(ys, e1);
        let fix = Box::new(Expr::PrimOp(OpCode::Fix));
        let e1 = Box::new(Expr::App(fix, vec![lam]));
        Expr::Let(f, e1, e2)
    },
    BinOp<SumExpr, CompOp, SumExpr>,
}

SumExpr: Expr = {
    BinOp<SumExpr, SumOp, ProductExpr>,
    ProductExpr,
}

ProductExpr: Expr = {
    BinOp<ProductExpr, ProductOp, AppExpr>,
    AppExpr,
}

AppExpr: Expr = {
    AtomExpr,
    Box<AtomExpr> AtomExpr+ => Expr::App(<>),
}

AtomExpr: Expr = {
    Name => Expr::Var(<>, None),
    Num => Expr::Num(<>),
    Bool => Expr::Bool(<>),
    "(" <Expr> ")",
    "fix" => Expr::PrimOp(OpCode::Fix),
    "{" <fes:List0<RecordAssign, ";">> "}" => {
        let (fs, es) = fes.into_iter().unzip();
        Expr::Record(fs, es)
    },
    <Box<AtomExpr>> "." <Name> => Expr::Proj(<>),
}

RecordAssign: (Name, Expr) = {
    <Name> "=" <Expr> => (<>),
}

Name: Name = {
    ID_LOWER => <>.to_string(),
}

Num: i64 = {
    NUMBER =>? i64::from_str(<>)
        .map_err(|_| ParseError::User {
            error: "number literal is too big"
        })
};
Bool: bool = {
    "false" => false,
    "true" => true,
}

SumOp: OpCode = {
    "+" => OpCode::Add,
    "-" => OpCode::Sub,
}

ProductOp: OpCode = {
    "*" => OpCode::Mul,
    "/" => OpCode::Div,
}

CompOp: OpCode = {
    "==" => OpCode::Equals,
    "!=" => OpCode::NotEq,
    "<" => OpCode::Less,
    "<=" => OpCode::LessEq,
    ">" => OpCode::Greater,
    ">=" => OpCode::GreaterEq,
}

Box<T>: Box<T> = {
    <T> => Box::new(<>),
}

List0<T, Sep>: Vec<T> = {
    => Vec::new(),
    <ts:(<T> Sep)*> <t:T> => {
        let mut ts = ts;
        ts.push(t);
        ts
    },
}

BinOp<X, Op, Y>: Expr = {
    <x:X> <op:Op> <y:Y> => Expr::App(Box::new(Expr::PrimOp(op)), vec![x, y]),
}

match {
    "fun",
    "let",
    "rec",
    "in",
    "fix",
    "if",
    "then",
    "else",
    "true",
    "false",
    r"[a-z]\w*" => ID_LOWER,
    r"[0-9]+" => NUMBER,
    r"\s*" => { }, // The default whitespace skipping is disabled an, `ignore pattern` is specified
    r"\(\*([^\*]*\*+[^\*\)])*([^\*]*\*+|[^\*])*\*\)" => { },  // Skip `(* comments *)`
    _
}
