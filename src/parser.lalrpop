use std::str::FromStr;

use crate::syntax::*;

grammar;

pub Expr: Box<Expr> = { // (1)
    Expr ExprOp Factor => Box::new(Expr::Op(<>)), // (2)
    Factor,
};

ExprOp: Opcode = { // (3)
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
};

Factor: Box<Expr> = {
    Factor FactorOp Term => Box::new(Expr::Op(<>)),
    Term,
};

FactorOp: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
};

Term: Box<Expr> = {
    Num => Box::new(Expr::Num(<>)), // (4)
    "(" <Expr> ")",
    <f:Fun> "(" <xs:(<Expr> ",")*> <x:Expr> ")" => {
        let mut xs = xs.into_iter().map(|x| *x).collect::<Vec<Expr>>();
        xs.push(*x);
        Box::new(Expr::Fun(f, xs))
    },
};

Fun: FunCode = {
    "fac" => FunCode::Fac,
    "mod" => FunCode::Rem,
};

Num: i64 = {
    r"[0-9]+" => i64::from_str(<>).unwrap()
};
