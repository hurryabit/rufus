use std::str::FromStr;

use crate::syntax::*;

grammar;

pub Expr: Expr = {
    "let" <Name> "=" <Box<Expr>> ";" <Box<Expr>> => Expr::Let(<>),
    <SimpleExpr>,
};

SimpleExpr: Expr = {
    Name => Expr::Var(<>, None),
    Num => Expr::Num(<>),
    <Opcode> "(" <Box<SimpleExpr>> "," <Box<SimpleExpr>> ")" => Expr::Op(<>),
    <f:Name> "(" <xs:CommaList0<SimpleExpr>> ")" => Expr::App(Box::new(Expr::Var( f, None)), xs),
    "|" <CommaList0<Name>> "|" <Box<Expr>> => Expr::Lam(<>),
    "print" "(" <Box<Expr>> ")" => Expr::Print(<>),
    "(" <SimpleExpr> ")",
    "{" <Expr> "}"
};

Name: Name = {
    r"[a-z]\w*" => Name(<>.to_string())
};

Num: i64 = {
    r"[0-9]+" => i64::from_str(<>).unwrap()
};

Opcode: Opcode = {
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
}

Box<T>: Box<T> = {
    <T> => Box::new(<>)
}

CommaList0<T>: Vec<T> = {
    => Vec::new(),
    <ts:(<T> ",")*> <t:T> => {
        let mut ts = ts;
        ts.push(t);
        ts
    }
}
