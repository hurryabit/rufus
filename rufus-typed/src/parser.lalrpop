use std::str::FromStr;
use lalrpop_util::ParseError;
use crate::syntax::*;

grammar;

pub Type: Type  = {
    TypeVar => Type::Var(<>),
    "Int" => Type::Int,
    "Bool" => Type::Bool,
    TypeCon => Type::Synonym(<>),
    "(" <List0<Type, ",">> ")" "->" <Box<Type>> => Type::Fun(<>),
    <TypeCon> "<" <List1<Type, ",">> ">" => Type::App(<>),
    "{" <List0<RecordFieldType, ",">> "}" => Type::Record(<>),
    "[" <List1<VariantCaseType, "|">> "]" => Type::Variant(<>),
}

TypeInParens: Type = {
    "(" <Type> ")" => <>,
}

RecordFieldType: (ExprVar, Type) = {
    <ExprVar> ":" <Type> => (<>),
}

VariantCaseType: (ExprCon, Option<Type>) = {
    ExprCon TypeInParens? => (<>),
}

pub Expr: Expr = {
    SumExpr,
    BinOp<SumExpr, CompOp, SumExpr>,
    // TODO(MH): Merge the two rules below.
    "fn" "(" <List0<Param<OptTypeAnn>, ",">> ")" <Box<BlockExpr>> => Expr::Lam(<>),
    "fn" "<" <tyvars: List1<TypeVar, ",">> ">" "(" <exvars: List0<Param<OptTypeAnn>, ",">> ")" <body: Box<BlockExpr>> => Expr::TypeAbs(tyvars, Box::new(Expr::Lam(exvars, body))),
    "if" <Box<Expr>> <Box<BlockExpr>> "else" <Box<BlockExpr>> => Expr::If(<>),
}

pub BlockExpr: Expr = {
    "{" <Statements> "}" => <>,
}

Statements: Expr = {
    Expr => <>,
    "let" <ExprVar> <OptTypeAnn> "=" <Box<Expr>> ";" <Box<Statements>> => Expr::Let(<>),
    "let" <ExprVar> <OptTypeAnn> "=" <Box<BlockExpr>> ";" <Box<Statements>> => Expr::Let(<>),
}

SumExpr: Expr = {
    BinOp<SumExpr, SumOp, ProductExpr>,
    ProductExpr,
}

ProductExpr: Expr = {
    BinOp<ProductExpr, ProductOp, AtomExpr>,
    AtomExpr,
}

AtomExpr: Expr = {
    ExprInParens => <>,
    ExprVar => Expr::Var(<>),
    Num => Expr::Num(<>),
    Bool => Expr::Bool(<>),
    // TODO(MH): Merge the two rules below.
    <fun: ExprVar> "(" <args: List0<Expr, ",">> ")" => Expr::App(Box::new(Expr::Var(fun)), args),
    // NOTE(MH): Ideally, we would not have the "@" symbol before the angle
    // brackets, but that would make the expression `f(g<a, b>(c))` ambiguous
    // to parse. With `f(g@<a, b>(c))` there's no ambiguity.
    <fun: ExprVar> "@" "<" <tyargs: List1<Type, ",">> ">" "(" <exargs: List0<Expr, ",">> ")" => Expr::App(Box::new(Expr::TypeApp(Box::new(Expr::Var(fun)), tyargs)), exargs),
    "{" <List0<RecordAssign, ",">> "}" => Expr::Record(<>),
    <Box<AtomExpr>> "." <ExprVar> => Expr::Proj(<>),
    <ExprCon> <Box<ExprInParens>?> => Expr::Variant(<>),
}

ExprInParens: Expr = {
    "(" <Expr> ")" => <>,
}

OptTypeAnn: Option<Type> = {
    => None,
    ":" <Type> => Some(<>),
}

Param<T>: (ExprVar, T) = {
    ExprVar T => (<>),
}

RecordAssign: (ExprVar, Expr) = {
    <ExprVar> "=" <Expr> => (<>),
}

TypeVar: TypeVar = {
    r"[a-z]\w*" => TypeVar::new(<>),
}

TypeCon: TypeCon = {
    r"[A-Z]\w*" => TypeCon::new(<>),
}

ExprVar: ExprVar = {
    r"[a-z]\w*" => ExprVar::new(<>),
}

ExprCon: ExprCon = {
    r"[A-Z]\w*" => ExprCon::new(<>),
    "Int" => ExprCon::new(<>),
    "Bool" => ExprCon::new(<>),
}

Num: i64 = {
    r"[0-9]+" =>? i64::from_str(<>)
        .map_err(|_| ParseError::User {
            error: "number literal is too big"
        })
};

Bool: bool = {
    "false" => false,
    "true" => true,
}

SumOp: OpCode = {
    "+" => OpCode::Add,
    "-" => OpCode::Sub,
}

ProductOp: OpCode = {
    "*" => OpCode::Mul,
    "/" => OpCode::Div,
}

CompOp: OpCode = {
    "==" => OpCode::Equals,
    "!=" => OpCode::NotEq,
    "<" => OpCode::Less,
    "<=" => OpCode::LessEq,
    ">" => OpCode::Greater,
    ">=" => OpCode::GreaterEq,
}

Box<T>: Box<T> = {
    <T> => Box::new(<>),
}

List0<T, Sep>: Vec<T> = {
    => Vec::new(),
    List1<T, Sep> => <>,
}

List1<T, Sep>: Vec<T> = {
    <ts: (<T> Sep)*> <t: T> Sep? => {
        let mut ts = ts;
        ts.push(t);
        ts
    },
}

BinOp<X, Op, Y>: Expr = {
    <x: Box<X>> <op: Op> <y: Box<Y>> => Expr::BinOp(<>),
}
